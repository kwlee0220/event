apply plugin: 'java-library'

group = 'etri'
version = '1.0'

sourceCompatibility = JavaVersion.VERSION_1_8
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
	mavenCentral()
}

ext {
	commons_beanutils_version = '1.9.3'
	mvel2_version = '2.4.14.Final'
	rxjava2_version = '2.2.21'
	
	groovy_version = '2.5.7'
}

dependencies {
	implementation project(":utils")
	
	implementation "org.codehaus.groovy:groovy:${groovy_version}"
	
	implementation "org.mvel:mvel2:${mvel2_version}"
	implementation "io.reactivex.rxjava2:rxjava:${rxjava2_version}"
	implementation "commons-beanutils:commons-beanutils:${commons_beanutils_version}"
	
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}

apply plugin: 'eclipse'
eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						if ( entry.path.startsWith("src/test/") ) {
							entry.output = 'test_classes'
						}
						else {
							entry.output = 'classes'
						}
					}
				}
			}
		}
	}
}